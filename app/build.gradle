plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.nrup.mygitactionsdemo'

    compileSdk 33

    defaultConfig {
        applicationId "com.nrup.mygitactionsdemo"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword System.getenv("ANDROID_RELEASE_STORE_PASSWORD") ?: RELEASE_STORE_PASSWORD
            keyAlias System.getenv("ANDROID_RELEASE_KEY_ALIAS") ?: RELEASE_KEY_ALIAS
            keyPassword System.getenv("ANDROID_RELEASE_KEY_PASSWORD") ?: RELEASE_KEY_PASSWORD
        }
    }

    // Build Types : Release for production and Debug for Testing.

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }

    }


    /**
     * Product flavors. Used to change the data like, BaseUrl / static pages based on different build.
     * Here, 6 Build variant generated. (2 Build Type * 3 Product Flavor)
     *
     * localDebug & localRelease
     * uatDebug &  uatRelease
     * productionDebug & productionRelease
     *
     * versionNameSuffix will shown in the Application->Version Name. ex. 1.0.prod
     * */

    flavorDimensions "version"
    productFlavors {

        production {
            versionNameSuffix ".prod"
            buildConfigField "String", "BASE_URL", BASE_URL_PRODUCTION
        }

        uat {
            versionNameSuffix ".uat"
            buildConfigField "String", "BASE_URL", BASE_URL_UAT
        }

        local {
            versionNameSuffix ".local"
            buildConfigField "String", "BASE_URL", BASE_URL_LOCAL
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }

}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}