plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.nrup.mygitactionsdemo'
    compileSdk 33

    // For View Binding
    buildFeatures {
        viewBinding = true
    }

    // For release build
    signingConfigs {
        MyGitActionsDemoConfigs {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "com.nrup.mygitactionsdemo"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // Flavours release/qa/debug

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        qa {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
        }

    }

    flavorDimensions "default"
    productFlavors {
        myGitActionsDemo {
            dimension "default"
            signingConfig signingConfigs.MyGitActionsDemoConfigs
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "debug") {
            //buildConfigField 'String', 'BASE_URL', "\"http://com.nrup.mygitactionsdemo/debug/GitActions/ApplicationService.svc/\""
            buildConfigField "String", "BASE_URL", BASE_URL_DEBUG

        } else if (variant.buildType.name == "qa") {
            //buildConfigField 'String', 'BASE_URL', "\"http://com.nrup.mygitactionsdemo/qa/GitActions/ApplicationService.svc/\""
            buildConfigField "String", "BASE_URL", BASE_URL_QA
        } else {
            //buildConfigField 'String', 'BASE_URL', "\"http://com.nrup.mygitactionsdemo/release/GitActions/ApplicationService.svc/\""
            buildConfigField "String", "BASE_URL", BASE_URL_RELEASE
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}